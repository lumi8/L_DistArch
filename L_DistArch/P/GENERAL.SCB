'---------------------------------------
' L_DistArch - Global Configuration
'---------------------------------------
' Purpose:
'   Centralized constants for redundancy associations and
'   station list. Search UPDATE for customize your application.

'---------------------------------------
' Windows API declarations
'---------------------------------------
' These functions are used by Restart_PcVue() and GetSvPath()
' to retrieve the current process ID and executable path.
'---------------------------------------


Declare Function GetCurrentProcessId Lib "kernel32" Alias "GetCurrentProcessId" () As Long;
Declare Function GetModuleFileNameEx Lib "psapi.dll" Alias "GetModuleFileNameExA" (hProcess As Long, hModule As Long, lpFilename As Long, nSize As Long) As Long;
Declare Function OpenProcess Lib "kernel32" (dwDesiredAccess As Long, bInheritHandle As Long, dwProcessId As Long) As Long;
Declare Function CloseHandle Lib "kernel32" (hObject As Long) As Long;


'---------------------------------------
' Entry point
'---------------------------------------
' Sub Main() is automatically executed at startup.
' It schedules cyclic tasks and delayed initialization.
'---------------------------------------

Sub Main()
	

    ' Schedule cyclic buzzer management
    CYCLIC("ADDPROG", 15, "L_DistArch/GENERAL.SCB", "", "Buzzer");

    ' Delay loading of Main2() to allow system initialization
    CYCLIC("ADDPROG", 10, "L_DistArch/GENERAL.SCB", "", "Main2");
    
    Program("PRELOAD", "L_DistArch/Networking.SCB", "");
    

End Sub


'---------------------------------------
' Main2
'---------------------------------------
' Purpose:
'   Secondary initialization routine executed after delay.
'   - Updates version, user, and active flag
'   - Registers event triggers for user and version changes
'   - Cleans up cyclic delay started in Main()
'---------------------------------------

Sub Main2()

    Dim VersionID  As Str;
    Dim DisconnID  As Str;
    Dim UserID     As Str;

    
    ' Optional dual monitor activation (currently disabled)
    ' DualDisplay()
    
    
    ' Remove delayed cyclic created in Main()
    CYCLIC("DEL_EX", 10, "L_DistArch/GENERAL.SCB", "", "Main2");
    
    ' Log module initialization
    TRACE("LOG", "***** L_ArchDist : Client-Server Management module loaded ****");

End Sub



'---------------------------------------
' UserReload
'---------------------------------------
' Purpose:
'   Event routine that manages:
'   - Reload of user file USER.DAT (USER_RELOAD variable)
'
' Variables:
'   <Architecture>.<Station>.USER_RELOAD  - Reload trigger for user.dat
'
' Notes: (Todo: find a way to replace this 15s cyclic by events)
'   - Variables are defined in Application Architect.
'   - Batch "user_load.bat" must exist in library folder \TP\
'     This copies the updated user.dat from the central folder to the
'     local station's project directory, only if newer.
'---------------------------------------

Sub UserReload()

    Dim VarReset        As Str;
    Dim VarUser         As Str;
    Dim LoadCmd         As Str;
    Dim VarRunningMode  As Str;
    Dim RunningMode     As Single;
    Dim RunService      As Single;
    Dim RunRDS          As Single;


    '-----------------------------------
    ' User reload management
    '-----------------------------------

   	TRACE("LOG", ADDSTRING("UserReload: reload requested by station ", @SYSTEM.STATION_NAME));

    ' Build batch path -> <ProjectDir><LibraryDir>\TP\user_load.bat
    LoadCmd = ADDSTRING(GETPROJECTDIR(), "\\LIB\\L_DistArch\\TP\\user_load.bat");

    TRACE("LOG", ADDSTRING("UserReload: executing reload with command ", LoadCmd));
        
    ' Launch system command (not available on WebVue)
    SYSTEM("SYSTEM", ADDSTRING("\"", LoadCmd, "\""));

    ' Reset flag
    Set(GetArg("VARNAME"), 0);
    Sendlist("BLOC");

    TRACE("LOG", ADDSTRING("UserReload: user reload completed on ", @System.Station_Name));

    '-----------------------------------
    ' Running mode update
    '-----------------------------------
    'VarRunningMode = ADDSTRING("LAN.", @SYSTEM.STATION_NAME, ".RUNNINGMODE");

    'RunService = TOS(@System.LocalHost.Environment.RunningAsService);
    'RunRDS     = TOS(@System.LocalHost.Environment.RunningInRemoteDesktopSession);

    ' Encode mode:
    '   1 = Console
    '   2 = Service
    '   3 = RDS
    'RunningMode = (1 + (RunService * 1) + (RunRDS * 2));
    '?VarRunningMode = RunningMode;

End Sub


'---------------------------------------
' Restart_PcVue
'---------------------------------------
' Purpose:
'   Restart PcVue runtime in different scenarios:
'   - Manual reset requested (LAN.<Station>.RESET)
'   - Service restart if running as Windows Service
'   - RDS (Remote Desktop Session) adjustments
'   - Normal restart using svrestart.exe
'
' Notes:
'   - Uses Windows API (GetCurrentProcessId, GetSvPath)
'   - Requires svrestart.exe in library folder \TP\
'   - If user is online, restart is postponed
'---------------------------------------

Sub Restart_PcVue()

    Dim SvProjectsPath  As Str;
    Dim SvProjectPath   As Str;
    Dim SvProjectName   As Str;
    Dim SvPath          As Str;    ' Path of sv32.exe
    Dim SvRestartPath   As Str;    ' Path of svrestart.exe
    Dim CommandLine     As Str;    ' Final command to execute
    Dim Args            As Str;    ' Arguments for svrestart.exe
    Dim MyId            As Long;   ' Current PcVue process ID
    Dim i               As Integer;
    Dim VarReset        As Str;
    Dim LenStName       As Integer;
    Dim LenPrjName      As Integer;
    Dim LenResult       As Integer;
    Dim ServiceRst      As Str;
    Dim VarOverRide     As Str;
    Dim bVarOverRide	As integer;

    TRACE("LOG","***** L_ArchDist : SCADA Reload request reiceved ****");

    '---------------------------------------
    ' Check if reload can be executed
    ' Conditions:
    '   - No user logged in ((CmpString(" ", @USER)!=0) 
    '   - Override flag set (<Architecture>.RST_OVERRIDE)
    '---------------------------------------
    VarOverRide = AddString(GetArg("ARG1"), ".RST_OVERRIDE");
    bVarOverRide = ?VarOverRide;
    
    IF ((CmpString(" ", @USER)==0) || bVarOverRide) THEN

        ' Reset <Architecture>.<Station>.RESET to 0 (acknowledge reload)
        VarReset = GetArg("VARNAME");
        ?VarReset = 0;

        ' Wait a few seconds to allow propagation across servers
        DELAY(3);

        TRACE("LOG", "***** L_ArchDist : SCADA Reload in progress ****");

        ' Get process ID and paths
        MyId = GetCurrentProcessId();
        SvProjectPath = GETPROJECTDIR();
        SvPath = GetSvPath(MyId);

        ' Extract project name from full project path
        While (ASC(Right(SvProjectPath , i++) ) != ASC("\\") ) 
        Wend 
        SvProjectName = Right( SvProjectPath , i - 2); SvProjectsPath = Left ( SvProjectPath , LEN (SvProjectPath ) - LEN (ADDSTRING ("USR\\" , SvProjectName)));
        
        '---------------------------------------
        ' Remote Desktop Session handling
        ' Adjust project name by removing station suffix (_<Station>)
        '---------------------------------------
        IF (@System.LocalHost.Environment.RunningInRemoteDesktopSession == 1) THEN
            LenStName = LEN(@SYSTEM.STATION_NAME); 
            LenStName=(LenStName + 1);	' Remove station name + underscore
            LenPrjName = LEN(SvProjectName);
            LenResult = (LenPrjName - LenStName);
            SvProjectName = Left(SvProjectName, LenResult);
        END IF

        '---------------------------------------
        ' Service mode handling
        ' If PcVue is running as service, restart via Windows services
        '---------------------------------------
        IF (@System.LocalHost.Environment.RunningAsService == 1) THEN
            ServiceRst = "net stop SvCoreDaemon && net start SvCoreDaemon";
            TRACE("LOG", ADDSTRING("***** L_ArchDist : SERVICE RESTART : ", ServiceRst, " ****"));
            SYSTEM("SYSTEM", ServiceRst);
            STOP();
        END IF

        '---------------------------------------
        ' Build restart command (svrestart.exe)
        '---------------------------------------
        SvRestartPath = ADDSTRING (SvProjectPath , "\\LIB\\L_DistArch\\TP\\svrestart.exe" ); 
        SvRestartPath = ADDSTRING ("\"" , SvRestartPath); 
        SvRestartPath = ADDSTRING (SvRestartPath,"\""); 
        
        Args = ADDSTRING ( " " , TOC(MyId) ); 
        Args = ADDSTRING (Args, " \""); 
        Args = ADDSTRING (Args , SvPath); 
        Args = ADDSTRING (Args , "\""); 
        Args = ADDSTRING (Args , " "); 
        Args = ADDSTRING (Args , "\"");
        
        ' RDS mode requires additional parameters
        IF (@System.LocalHost.Environment.RunningInRemoteDesktopSession == 1) THEN
        	Args = ADDSTRING (Args , ADDSTRING ( "-WTS ",SvProjectName ) ); 
        	Args = ADDSTRING (Args , ADDSTRING ( ",",@SYSTEM.STATION_NAME) ); 
        	Args = ADDSTRING (Args, " -r"); 'Args = ADDSTRING (Args, " -k 1,1,1 -r"); 
        ELSE 
        	Args = ADDSTRING (Args, " -r"); 
        END IF
        
        Args = ADDSTRING (Args , "\""); 
        Args = ADDSTRING (Args , " "); 
        Args = ADDSTRING (Args , "\""); 
        Args = ADDSTRING (Args , "Please wait, project "); 
        Args = ADDSTRING (Args , SvProjectName); 
        Args = ADDSTRING (Args , " restart in progress..."); 
        Args = ADDSTRING (Args , "\"");
        
        CommandLine = ADDSTRING(SvRestartPath, Args);

        ' Launch restart application
        ' Comment this line during test to avoid immediate restart
        APPLICATION("LOAD", CommandLine);

        ' Exit current PcVue process
        SYSTEM("EXIT");

    ELSE
        '---------------------------------------
        ' If user is logged in -> postpone reload
        '---------------------------------------
        TRACE("LOG", "***** L_ArchDist : User online, reload postponed... ****");
    END IF

End Sub


'---------------------------------------
' GetSvPath
'---------------------------------------
' Purpose:
'   Retrieve the full executable path of the current PcVue process.

' Notes:
'   - Uses Windows API functions declared globally:
'       OpenProcess(), GetModuleFileNameEx(), CloseHandle()
'   - Allocates and frees a temporary buffer to store the path
'---------------------------------------

Sub GetSvPath(ProcessID)

      Dim lBufProc as Long;
      Dim lBufName as Long;
      Dim lMaxSize as Long;
      Dim sRet as Str;

      ' Allocate a buffer for the executable path
      lMaxSize = 255;
      lBufName  = ALLOC_BUFFER( lMaxSize );
      
      ' Open process handle
      lBufProc  = OpenProcess(  1040 , 0  , ProcessID );
      
      ' Query full module file name (path to sv32.exe)
      GetModuleFileNameEx(   lBufProc , 0, lBufName  , lMaxSize );
      
      ' Close handle
      CloseHandle( lBufProc  );
      
      ' Copy buffer content into string and trim spaces
      sRet = CGET_BUFFER( lBufName  , 0, lMaxSize );
      FREE_BUFFER (lBufName);
      
      Return ( LTRIM (sRet) );

End Sub


'---------------------------------------
' Buzzer
'---------------------------------------
' Purpose:
'   Play buzzer sound if both enable and trigger flags are active.
'
' Variables: (Todo: find a way to get variables names dynamically)
'   LAN.<Station>.EN_BUZZER  - Enable flag for buzzer
'   LAN.<Station>.BUZZER     - Trigger flag for buzzer
'
' Notes:
'   - Uses wav_player.exe located in library folder \TP\
'   - Source code available on GitHub: https://github.com/lumi8/wav_player
'   - Sound file: suonoalto.wav
'---------------------------------------

Sub Buzzer()

	Dim ProjectPath as Str;
	Dim VarEnBuzzer as Str;
	Dim VarBuzzer as Str;

	'VarEnBuzzer=ADDSTRING("LAN.",@SYSTEM.STATION_NAME,".EN_BUZZER");
	'VarBuzzer=ADDSTRING("LAN.",@SYSTEM.STATION_NAME,".BUZZER");

	'IF (?VarEnBuzzer == 1 && ?VarBuzzer == 1) THEN

		ProjectPath=GETPROJECTDIR();
		ProjectPath=ADDSTRING(ProjectPath,"\\LIB\\L_DistArch\\TP\\wav_player.exe");
	''	APPLICATION("LOAD",ProjectPath,"suonoalto.wav",5);

	'END IF

End Sub


'---------------------------------------
' SetBuzzer
'---------------------------------------
' Purpose:
'   Set buzzer trigger flag (BUZZER = 1) if buzzer is enabled.
'
' Variables: (Todo: find a way to get variables names dynamically)
'   LAN.<Station>.EN_BUZZER  - Enable flag for buzzer
'   LAN.<Station>.BUZZER     - Trigger flag for buzzer
'---------------------------------------

Sub SetBuzzer()

	Dim ProjectPath as Str;
	Dim VarEnBuzzer as Str;
	Dim VarBuzzer as Str;

	'VarEnBuzzer=ADDSTRING("LAN.",@SYSTEM.STATION_NAME,".EN_BUZZER");
	'VarBuzzer=ADDSTRING("LAN.",@SYSTEM.STATION_NAME,".BUZZER");

	'IF (?VarEnBuzzer == 1 ) THEN

	''	?VarBuzzer = (1);

	'END IF

End Sub


'---------------------------------------
' ResetBuzzer
'---------------------------------------
' Purpose:
'   Reset buzzer trigger flag (BUZZER = 0).
'
' Variables: (Todo: find a way to get variables names dynamically)
'   LAN.<Station>.BUZZER     - Trigger flag for buzzer
'---------------------------------------

Sub ResetBuzzer()

	Dim ProjectPath as Str;
	Dim VarEnBuzzer as Str;
	Dim VarBuzzer as Str;

	'VarBuzzer=ADDSTRING("LAN.",@SYSTEM.STATION_NAME,".BUZZER");
	'?VarBuzzer = (0);

End Sub


'---------------------------------------
' UserDisconnection
'---------------------------------------
' Purpose:
'   Force user logout when <Architecture>.<Station>.USER_DISC is triggered.
'
' Variables:
'   <Architecture>.<Station>.USER_DISC  - Trigger flag for user disconnection
'
' Notes: 
'   - Trigger is defined in Application Architect.
'   - Resets USER_DISC flag to 0 after logout.
'---------------------------------------

Sub UserDisconnection()
	
	Dim strDisconn as Str;
	
	' Force logout
	SYSTEM("LOGOUT");
	TRACE("LOG",ADDSTRING("User disconnection override ",@USER));
	
	strDisconn = GetArg("VARNAME");
	?strDisconn = 0;

End Sub


'---------------------------------------
' BroadcastUserMgt
'---------------------------------------
' Purpose:
'   Distribute user database updates (user.dat) to all stations.
'   - Runs user_copy.bat on the local station to copy user.dat 
'     into the central shared folder.
'   - Sets LAN.<Station>.USER_RELOAD = 1 on all other stations,
'     triggering their reload procedure.
'
' Variables:
'   LAN.<Station>.USER_RELOAD - Reload trigger for user database
'
' Notes:
'   - Station list is defined in Stations[] global array.
'   - Batch file "user_copy.bat" must be present in library folder \TP\
'   - The batch uses robocopy to copy the latest user.dat to the central folder.
'   - Each client/server then reloads the updated file when USER_RELOAD = 1.
'---------------------------------------

Sub BroadcastUserMgt()

    Dim VarName as Str;
    Dim StationName as Str;
    Dim i as Integer;
    Dim CopyCommand as Str;
    Dim FileName as Str;
    Dim lngBuffer1 as Long;
    Dim Line as str;

	' Execute batch to copy user.dat from local station to central folder
	CopyCommand = ADDSTRING(GETPROJECTDIR(),"\\LIB\\L_DistArch\\TP\\user_copy.bat");
	SYSTEM("SYSTEM", ADDSTRING("\"", CopyCommand, "\""));
	TRACE("LOG", ADDSTRING("BroadcastUserMgt: distributed user.dat from ", @SYSTEM.STATION_NAME));
	
	' Notify other stations to reload user.dat
	FileName = ADDSTRING(GETPROJECTDIR(),"\\TP\\DistArchStations.txt");

	Fopen(FileName, "r");
	
	While (Feof(FileName)==0)
		VarName = Fgets(FileName, 255);
		lngBuffer1 = Alloc_Buffer(255);
		Put_Buffer(lngBuffer1, 0, VarName);
		StationName = Asciifield("STR", lngBuffer1, Asciifield("COUNT", lngBuffer1, ".") , ".");
		print(StationName);
		
		If ((CmpString(StationName, @System.Station_name)!=0) && (CmpString(StationName, "")!=0)) Then
			VarName = AddString(VarName, ".USER_RELOAD");
			?Varname = 1;		
		End If
		Free_Buffer(lngBuffer1);		
	Wend
	Fclose(FileName);
	

End Sub


'---------------------------------------
' DualDisplay
'---------------------------------------
' Purpose:
'   Activate dual monitor layout on specific stations.
'   - Opens main mimic "MainPage" on secondary screen (Region 2).
'   - Ensures focus returns to primary screen after execution.
'
' Variables:
'   @SYSTEM.STATION_NUMBER - Used to identify stations that require
'                            dual display (edit conditions as needed).
'
' Notes:
'   - Requires dual monitor configuration at OS level.
'   - Edit station numbers in IF condition to match project setup.
'   - Region(SETREGION, n) selects which screen region to activate.
'   - Window(OPEN, "MainPage", "") opens the mimic on the given region.
'---------------------------------------

Sub DualDisplay()

    ' Enable dual display only for selected stations
    IF ((@SYSTEM.STATION_NUMBER == 1) OR (@SYSTEM.STATION_NUMBER == 2)) THEN

        TRACE("LOG", "***** L_ArchDist : Dual monitor activated ****");

        ' Switch to secondary screen (Region 2)
        REGION("SETREGION", 2);

        ' Open the main mimic on the second monitor
        WINDOW("OPEN", "MainPage", "");

    END IF

    ' Remove this cyclic task (only runs once after startup)
    CYCLIC("DEL", 10, "L_DistArch/GENERAL.SCB", "", "DualDisplay");

    ' Reset selection and restore focus to primary screen
    REGION("SETSELECTION", 0);
    REGION("SETREGION", 1);

End Sub


'---------------------------------------
' H_Switch
'---------------------------------------
' Purpose:
'   Switch active/passive servers for historical data redundancy.
'   - Assigns 100% availability to the active station.
'   - Assigns 50% availability to the passive station (hot-standby).
'
' Arguments:
'   ARG1 = Association name (historical redundancy)
'   ARG2 = Active station name
'   ARG3 = Passive station name
'
' Notes:
'   - LAN("SET_AVAILABLE_RATE", ...) is used for load balancing.
'   - Both servers remain online; passive is ready to take over.
'---------------------------------------

Sub H_Switch()

    Dim cAssociation      As Str;
    Dim cStationName      As Str;
    Dim cOtherStationName As Str;
    Dim iReturn           As Integer;

    ' Retrieve arguments passed to this sub
    cAssociation      = Ucase(GetArg("ARG1"));
    cStationName      = Ucase(GetArg("ARG2"));
    cOtherStationName = Ucase(GetArg("ARG3"));

	'LAN (Mode, AssocName, StationName, AvailableRate);

	' Set availability: active = 100%, passive = 50%
	LAN("SET_AVAILABLE_RATE", cAssociation, cStationName,100 );
	LAN("SET_AVAILABLE_RATE", cAssociation, cOtherStationName,50 );
	
	TRACE("LOG", ADDSTRING("H_Switch: ", cAssociation, " active=", cStationName, " passive=", cOtherStationName));
	
End Sub


'---------------------------------------
' LogUser
'---------------------------------------
' Purpose:
'   Handle user login/logout events in redundant client-server setups.
'   - Triggers LAN.<Branch>.LOGIN or LAN.<Branch>.LOGOUT variables
'   - Stores user name, station, and running mode
'   - Adds cyclic program ResetTrigger() to reset the trigger bit
'
' Arguments:
'   BRANCH = Base branch name where LOGIN/LOGOUT variables are defined
'
' Notes:
'   - RunningMode is encoded as:
'       1 = CONSOLE
'       2 = SERVICE
'       3 = RDS
'   - Association name for redundancy is taken from ASSOC_RT
'   - LAN variables must exist in Application Architect
'---------------------------------------

Sub LogUser()

	Dim currBrch as Str;
	Dim varLogin as Str;
	Dim varLogout as Str;
	Dim varUser as Str;
	Dim varOnline as Str;
	Dim varStation as Str;
	Dim strVarUser as Str;
	Dim varTrigger as Str;
	Dim varRunningMode as Str;
	Dim RunningMode as Single;

	' Check if this station is active in the real-time association
	IF (@System.ASSOC_RT.LocalHost == 1) Then  					
	
		'Retrieve branch passed as argument
        currBrch = GETARG("BRANCH");

        ' Build LAN variable names
		varLogin       = ADDSTRING(currBrch, ".LOGIN"); 		
		varLogout      = ADDSTRING(currBrch, ".LOGOUT"); 		
		varOnline      = ADDSTRING(currBrch, ".ONLINE"); 	
		varUser        = ADDSTRING(currBrch, ".USER");
		varRunningMode = ADDSTRING(currBrch, ".RUNNINGMODE");
		varStation     = VARIABLE("TATT",varOnline,3);

		strVarUser = (?varUser); 								'put on varUSer the current user name
		RunningMode = (?varRunningMode); 						'put RunningMode value on internal Var

        '---------------------------------------
        ' User LOGIN case
        '---------------------------------------		
        IF ((LEN(strVarUser) > 1)) Then

			VARIABLE("SETTATT", VarLogin, 3, strVarUser);
			VARIABLE("SETTATT", VarLogin, 4, varStation);
		
            IF (RunningMode == 1) THEN VARIABLE("SETTATT", varLogin, 5, "CONSOLE"); END IF
            IF (RunningMode == 2) THEN VARIABLE("SETTATT", varLogin, 5, "SERVICE"); END IF
            IF (RunningMode == 3) THEN VARIABLE("SETTATT", varLogin, 5, "RDS");     END IF

			TRACE("LOG",(ADDSTRING("--- REMOTE TRACE LOGIN --- ",varLogin, " - ", strVarUser)));
			CYCLIC("ADDPROG", 1,"L_DistArch/GENERAL.SCB", currBrch, "ResetTrigger", "LOGIN" );
			?VarLogin = (1);

        '---------------------------------------
        ' User LOGOUT case
        '---------------------------------------
		ELSE

			VARIABLE("SETTATT", VarLogout, 3, strVarUser);
			VARIABLE("SETTATT", VarLogout, 4, varStation);
		
            IF (RunningMode == 1) THEN VARIABLE("SETTATT", varLogout, 5, "CONSOLE"); END IF
            IF (RunningMode == 2) THEN VARIABLE("SETTATT", varLogout, 5, "SERVICE"); END IF
            IF (RunningMode == 3) THEN VARIABLE("SETTATT", varLogout, 5, "RDS");     END IF
            		
			TRACE("LOG",(ADDSTRING("--- REMOTE TRACE LOGOFF --- ",varLogout, " - ", strVarUser)));
			CYCLIC("ADDPROG", 1,"L_DistArch/GENERAL.SCB", currBrch, "ResetTrigger", "LOGOUT" );
			?varLogout = (1);

		END IF

	END IF

End Sub


'---------------------------------------
' ResetTrigger
'---------------------------------------
' Purpose:
'   Reset trigger bits for LOGIN/LOGOUT events.
'   - Clears LAN.<Branch>.LOGIN or LAN.<Branch>.LOGOUT
'   - Ensures events are single-shot
'
' Arguments:
'   BRANCH = Base branch name
'   ARG1   = "LOGIN" or "LOGOUT"
'---------------------------------------

Sub ResetTrigger()

	Dim currBrch as Str;
	Dim varName as Str;
	Dim strArg1 as Str;
	
	strArg1 = GETARG("ARG1");
	currBrch = GETARG("BRANCH");
	
	CYCLIC("DEL", 1, "L_DistArch/GENERAL.SCB", currBrch, "ResetTrigger" );
	
	'Varname costruisce la variabile bit che triggera la scrittura evento
	varName=ADDSTRING(currBrch, ".",strArg1); 				
	?varName = (0);

	TRACE("LOG",(ADDSTRING("--- TRACE LOGINLOGOFF RESETTRIGGER --- ",varName)));
	
End Sub


'---------------------------------------
' DualHardCopy
'---------------------------------------
' Purpose:
'   Take a screenshot of the current mimic window.
'   - Supports dual monitor setups
'   - Saves PNG files in project folder \HARDCOPY\
'
' Notes:
'   - Requires nircmd.exe in \TP\ folder of the library
'   - File names include station name and timestamp
'   - Region 1 = primary monitor, Region 2 = secondary monitor
'---------------------------------------
Sub DualHardCopy()

	Dim PrintLink as Str;
	Dim Filename as Str;

	' Path to nircmd.exe (must exist in \L_DistArch\TP\)
	PrintLink = GETPROJECTDIR();
	PrintLink = ADDSTRING(PrintLink,"\\LIB\\L_DistArch\\TP\\nircmd.exe");
	
    ' Build screenshot filename: PROJECT\HARDCOPY\StationName-YYYY-MM-DD-HH-MM-SS.png
	Filename = ADDSTRING("savescreenshot ","\"",GETPROJECTDIR(),"\\HARDCOPY\\");
	Filename = ADDSTRING(Filename,@SYSTEM.STATION_NAME,"-");
	Filename = ADDSTRING(FileName, TOC(DATETIME("YEAR")), "-" , TOC(DATETIME("MONTH")),"-",TOC(DATETIME("DAY")),"-",TOC(DATETIME("HOUR")));
	Filename = ADDSTRING(FileName,"-",TOC(DATETIME("MINUTE")),"-",TOC(DATETIME("SECOND")),".png\"",);

    '---------------------------------------
    ' Select screenshot region based on current window region
    '---------------------------------------
	IF (WINDOW("GETREGION",WINDOW("CURRENTNAME"),WINDOW("CURRENTBRANCH")) == 1) Then
	
		' Region 1 -> Primary monitor
		Filename = ADDSTRING(Filename, " 0 0 1920 1080");
		APPLICATION ("LOAD" , PrintLink , FileName);
		
	ELSE
	
		IF (WINDOW("GETREGION",WINDOW("CURRENTNAME"),WINDOW("CURRENTBRANCH")) == 2) Then
		
			' Region 2 -> Secondary monitor
			Filename = ADDSTRING(Filename, " 1921 0 3840 1080");
			APPLICATION ("LOAD" , PrintLink , FileName);
		END IF
	
	END IF
	
End Sub

